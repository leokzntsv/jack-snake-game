class SnakeGame {
    field boolean debug;
    field GameBoard gameBoard;
    field Snake snake;
    field int currentDirection; // the snake's current direction: 
                                // 0 = none, 1 = up, 2 = down, 3 = left, 4 = right
    field int foodTarget;
    field int foodCnt;

    constructor SnakeGame new(boolean d, int target) {
        var int boardCellSize;
        var int initialSnakeLength;

        let debug = d;
        let foodTarget = target;
        let foodCnt = 0;

        let boardCellSize = 15; // min: 4 pixels
        let initialSnakeLength = 1;

        let gameBoard = GameBoard.new(boardCellSize);
        let snake = Snake.new(
            initialSnakeLength,
            initialSnakeLength + foodTarget,
            boardCellSize,
            gameBoard
        );
        let currentDirection = 0;
        return this;
    }

    // Public Methods

    method void dispose() {
        do gameBoard.dispose();
        do snake.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;
        let exit = false;

        while (~exit) {
            while ((key = 0) & (~exit)) {
                let key = Keyboard.keyPressed();
                if (~(_moveSnake())) {
                    let exit = true;
                }
            }

            if (key = 81) { let exit = true; } // q key
            if ((key = 88) & debug) { do snake.increaseLength(); } // x key

            if (key = 131) { do _updateDirection(1); } // up arrow
            if (key = 133) { do _updateDirection(2); } // down arrow
            if (key = 130) { do _updateDirection(3); } // left arrow
            if (key = 132) { do _updateDirection(4); } // right arrow

            while ((~(key = 0)) & (~exit)) {
                let key = Keyboard.keyPressed();
                if (~(_moveSnake())) {
                    let exit = true;
                }
            }
        }

        return;
    }

    // Private Methods

    method boolean _moveSnake() {
        var int nextCellType;
        var boolean didMove;

        if (currentDirection = 0) {
            let didMove = true;
        }

        if (currentDirection = 1) {
            let didMove = _moveSnakeUp();
        }

        if (currentDirection = 2) {
            let didMove = _moveSnakeDown();
        }

        if (currentDirection = 3) {
            let didMove = _moveSnakeLeft();
        }

        if (currentDirection = 4) {
            let didMove = _moveSnakeRight();
        }

        if (_isGameFinished()) {
            do _printCongrats();
            let didMove = false;
        }

        do Sys.wait(200); // delays the next movement
        return didMove;
    }

    method boolean _moveSnakeUp() {
        var int nextCellType;
        let nextCellType = gameBoard.nextCellUp(snake);
        if (nextCellType = 1) {
            if (debug) {
                do Output.printString("Move Up. Crash!");
                do Output.println();
            } else {
                do Output.printString("Game Over!");
                do Output.println();
                return false;
            }
        } else {
            do snake.moveUp();
            if (nextCellType = 2) {
                let foodCnt = foodCnt + 1;
                do snake.increaseLength();
                do gameBoard.placeFood();
            }
        }
        return true;
    }

    method boolean _moveSnakeDown() {
        var int nextCellType;
        let nextCellType = gameBoard.nextCellDown(snake);
        if (nextCellType = 1) {
            if (debug) {
                do Output.printString("Move Down. Crash!");
                do Output.println();
            } else {
                do Output.printString("Game Over!");
                do Output.println();
                return false;
            }
        } else {
            do snake.moveDown();
            if (nextCellType = 2) {
                let foodCnt = foodCnt + 1;
                do snake.increaseLength();
                do gameBoard.placeFood();
            }
        }
        return true;
    }

    method boolean _moveSnakeLeft() {
        var int nextCellType;
        let nextCellType = gameBoard.nextCellLeft(snake);
        if (nextCellType = 1) {
            if (debug) {
                do Output.printString("Move Left. Crash!");
                do Output.println();
            } else {
                do Output.printString("Game Over!");
                do Output.println();
                return false;
            }
        } else {
            do snake.moveLeft();
            if (nextCellType = 2) {
                let foodCnt = foodCnt + 1;
                do snake.increaseLength();
                do gameBoard.placeFood();
            }
        }
        return true;
    }

    method boolean _moveSnakeRight() {
        var int nextCellType;
        let nextCellType = gameBoard.nextCellRight(snake);
        if (nextCellType = 1) {
            if (debug) {
                do Output.printString("Move Right. Crash!");
                do Output.println();
            } else {
                do Output.printString("Game Over!");
                do Output.println();
                return false;
            }
        } else {
            do snake.moveRight();
            if (nextCellType = 2) {
                let foodCnt = foodCnt + 1;
                do snake.increaseLength();
                do gameBoard.placeFood();
            }
        }
        return true;
    }

    method boolean _isGameFinished() {
        return foodCnt = foodTarget;
    }

    method void _printCongrats() {
        do Output.printString("You've reached the target score of ");
        do Output.printInt(foodTarget);
        do Output.printString(".");
        do Output.println();
        do Output.printString("Congratulations!");
        do Output.println();
        return;
    }

    method void _updateDirection(int newDirection) {
        var int oppositeDirection;
        let oppositeDirection = _getOppositeDirection(currentDirection);

        if (~(newDirection = oppositeDirection)) {
            let currentDirection = newDirection;
        }

        return;
    }

    method int _getOppositeDirection(int direction) {
        if (direction = 1) { return 2; }
        if (direction = 2) { return 1; }
        if (direction = 3) { return 4; }
        if (direction = 4) { return 3; }
        return 0;
    }
}