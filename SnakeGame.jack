class SnakeGame {
    field GameBoard gameBoard;
    field Snake snake;
    field int currentDirection; // the snake's current direction: 
                                // 0 = none, 1 = up, 2 = down, 3 = left, 4 = right

    constructor SnakeGame new() {
        var int boardCellSize;
        let boardCellSize = 15;

        let gameBoard = GameBoard.new(boardCellSize);
        let snake = Snake.new(4, 20, boardCellSize, gameBoard);
        let currentDirection = 0;
        return this;
    }

    method void dispose() {
        // TODO: dispose objects
        do board.dispose();
        do snake.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;
        let exit = false;

        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
                do moveSnake();
            }

            if (key = 81) { let exit = true; }           // q key
            if (key = 88) { do snake.increaseLength(); } // x key

            if (key = 131) { do updateDirection(1); } // up arrow
            if (key = 133) { do updateDirection(2); } // down arrow
            if (key = 130) { do updateDirection(3); } // left arrow
            if (key = 132) { do updateDirection(4); } // right arrow

            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                do moveSnake();
            }
        }

        return;
    }

    method void moveSnake() {
        if (currentDirection = 1) { 
            if (gameBoard.canMoveUp(snake)) {
                do snake.moveUp();
            } else {
                do Output.printString("Move Up. Crash!");
                do Output.println();
            }
        }
        
        if (currentDirection = 2) {
            if (gameBoard.canMoveDown(snake)) {
                do snake.moveDown();
            } else {
                do Output.printString("Move Down. Crash!");
                do Output.println();
            }
        }

        if (currentDirection = 3) {
            if (gameBoard.canMoveLeft(snake)) {
                do snake.moveLeft();
            } else {
                do Output.printString("Move Left. Crash!");
                do Output.println();
            }
        }
        
        if (currentDirection = 4) {
            if (gameBoard.canMoveRight(snake)) {
                do snake.moveRight();
            } else {
                do Output.printString("Move Right. Crash!");
                do Output.println();
            }
        }

        do Sys.wait(250);  // delays the next movement
        return;
    }

    method void updateDirection(int newDirection) {
        var int oppositeDirection;
        let oppositeDirection = getOppositeDirection(currentDirection);

        if (~(newDirection = oppositeDirection)) {
            let currentDirection = newDirection;
        }

        return;
    }

    method int getOppositeDirection(int direction) {
        if (direction = 1) { return 2; }
        if (direction = 2) { return 1; }
        if (direction = 3) { return 4; }
        if (direction = 4) { return 3; }
        return 0;
    }
}