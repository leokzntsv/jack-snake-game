class GameBoard {
    field int cellSize;
    field int rows;
    field int cols;
    field Array board;  // 34x17 grid: 0 – empty cell;
                        //             1 – snake's body;
                        //             2 – food

    constructor GameBoard new(int size) {
        let cellSize = size;
        do _initializeBoard();
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method boolean canMoveUp(Snake snake) {
        var Array snakeX;
        var Array snakeY;
        var Array row;
        var int snakeHeadX;
        var int snakeHeadY;
        var int boardValue;

        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let snakeHeadX = snakeX[0];
        let snakeHeadY = snakeY[0];

        if (~(snakeHeadY > 0)) {
            return false;
        }

        let row = board[snakeHeadY - 1];
        let boardValue = row[snakeHeadX];

        return ~(boardValue = 1);
    }

    method boolean canMoveDown(Snake snake) {
        var Array snakeX;
        var Array snakeY;
        var Array row;
        var int snakeHeadX;
        var int snakeHeadY;
        var int boardValue;

        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let snakeHeadX = snakeX[0];
        let snakeHeadY = snakeY[0];

        if (~(snakeHeadY + 1 < rows)) {
            return false;
        }

        let row = board[snakeHeadY + 1];
        let boardValue = row[snakeHeadX];

        return ~(boardValue = 1);
    }

    method boolean canMoveLeft(Snake snake) {
        var Array snakeX;
        var Array snakeY;
        var Array row;
        var int snakeHeadX;
        var int snakeHeadY;
        var int boardValue;

        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let snakeHeadX = snakeX[0];
        let snakeHeadY = snakeY[0];

        if (~(snakeHeadX > 0)) {
            return false;
        }

        let row = board[snakeHeadY];
        let boardValue = row[snakeHeadX - 1];

        return ~(boardValue = 1);
    }

    method boolean canMoveRight(Snake snake) {
        var Array snakeX;
        var Array snakeY;
        var Array row;
        var int snakeHeadX;
        var int snakeHeadY;
        var int boardValue;

        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let snakeHeadX = snakeX[0];
        let snakeHeadY = snakeY[0];

        if (~(snakeHeadX + 1 < cols)) {
            return false;
        }

        let row = board[snakeHeadY];
        let boardValue = row[snakeHeadX + 1];

        return ~(boardValue = 1);
    }

    method void snakeDidInitialize(Snake snake) {
        var int length;
        var Array snakeX;
        var Array snakeY;
        var int i;
        var Array row;

        let length = snake.getLength();
        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let i = 0;
        while (i < length) {
            let row = board[snakeY[i]];
            let row[snakeX[i]] = 1;
            let i = i + 1;
        }

        return;
    }

    method void snakeWillMove(Snake snake) {
        do _removeSnakeFromBoard(snake);
        return;
    }

    method void snakeDidMove(Snake snake) {
        do _addSnakeToBoard(snake);
        return;
    }

    method void snakeDidIncreaseLength(Snake snake) {
        do _addSnakeToBoard(snake);
        return;
    }

    // Private Methods

    method void _initializeBoard() {
        var Array row;
        var int i;
        var int j;

        let rows = 255 / cellSize;
        let cols = 511 / cellSize;
        let board = Array.new(rows);

        let i = 0;
        while (i < rows) {
            let row = Array.new(cols);

            let j = 0;
            while (j < cols) {
                let row[j] = 0;
                let j = j + 1;
            }

            let board[i] = row;
            let i = i + 1;
        }

        return;
    }

    method void _removeSnakeFromBoard(Snake snake) {
        var int length;
        var Array snakeX;
        var Array snakeY;
        var int i;
        var Array row;

        let length = snake.getLength();
        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let i = 0;
        while (i < length) {
            let row = board[snakeY[i]];
            let row[snakeX[i]] = 0;
            let i = i + 1;
        }

        return;
    }

    method void _addSnakeToBoard(Snake snake) {
        var int length;
        var Array snakeX;
        var Array snakeY;
        var int i;
        var Array row;

        let length = snake.getLength();
        let snakeX = snake.getXCoordinates();
        let snakeY = snake.getYCoordinates();

        let i = 0;
        while (i < length) {
            let row = board[snakeY[i]];
            let row[snakeX[i]] = 1;
            let i = i + 1;
        }

        return;
    }
}