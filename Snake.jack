class Snake {
    field Array x, y;       // snake's x and y coordinates
    field int length;
    field int maxLength;
    field int size;         // snake's width in pixels
    
    field GameBoard board;

    constructor Snake new(int l, int maxL, int s, GameBoard gameBoard) {
        let length = l;
        let maxLength = maxL;
        let size = s;

        let x = Array.new(maxLength);
        let y = Array.new(maxLength);

        let board = gameBoard;

        do _initializeSnake();
        do _draw();

        return this;
    }

    // Public Methods

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method int getLength() {
        return length;
    }

    method Array getXCoordinates() {
        return x;
    }

    method Array getYCoordinates() {
        return y;
    }

    method void moveUp() {
        do board.snakeWillMove(this);

        if (((y[0] * size - size) > 0) | ((y[0] * size - size) = 0)) {
            do _erase();
            do _shiftBody();

            let y[0] = y[0] - 1;
            do _draw();
        }

        do board.snakeDidMove(this);
        return;
    }

    method void moveDown() {
        do board.snakeWillMove(this);

        if (((y[0] * size + size) < (255 - size)) | ((y[0] * size + size) = (255 - size))) {
            do _erase();
            do _shiftBody();

            let y[0] = y[0] + 1;
            do _draw();
        }

        do board.snakeDidMove(this);
        return;
    }

    method void moveLeft() {
        do board.snakeWillMove(this);

        if (((x[0] * size - size) > 0) | ((x[0] * size - size) = 0)) {
            do _erase();
            do _shiftBody();

            let x[0] = x[0] - 1;
            do _draw();
        }

        do board.snakeDidMove(this);
        return;
    }

    method void moveRight() {
        do board.snakeWillMove(this);

        if (((x[0] * size + size) < (511 - size)) | ((x[0] * size + size) = (511 - size))) {
            do _erase();
            do _shiftBody();

            let x[0] = x[0] + 1;
            do _draw();
        }

        do board.snakeDidMove(this);
        return;
    }

    method void increaseLength() {
        if (length < maxLength) {
            let x[length] = x[(length - 1)];
            let y[length] = y[(length - 1)];
            let length = length + 1;
            do _draw();
            do board.snakeDidIncreaseLength(this);
        }
        return;
    }

    // Private Methods

    method void _initializeSnake() {
        var int i;
        let i = 0;
        while (i < length) {
            let x[i] = (length - 1 - i);
            let y[i] = 0;
            let i = i + 1;
        }

        do board.snakeDidInitialize(this);
        return;
    }

    method void _draw() {
        var int i;
        let i = 0;
        do Screen.setColor(true);

        while (i < length) {
            do Screen.drawRectangle(
                x[i] * size + 1,
                y[i] * size + 1,
                x[i] * size + size - 1,
                y[i] * size + size - 1
            );
            let i = i + 1;
        }
        return;
    }

    method void _erase() {
        var int i;
        let i = 0;
        do Screen.setColor(false);

        while (i < length) {
            do Screen.drawRectangle(
                x[i] * size,
                y[i] * size,
                x[i] * size + size,
                y[i] * size + size
            );
            let i = i + 1;
        }
        return;
    }

    method void _shiftBody() {
        var int i;
        let i = length - 1;
        while (i > 0) {
            let x[i] = x[i - 1];
            let y[i] = y[i - 1];
            let i = i - 1;
        }
        return;
    }
}