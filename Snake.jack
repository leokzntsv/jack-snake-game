class Snake {
    field Array x, y;
    field int length;
    field int size;

    constructor Snake new() {
        var int arrayLength;
        let arrayLength = 10;
        let x = Array.new(arrayLength);
        let y = Array.new(arrayLength);

        let length = 4;

        let x[0] = 45;
        let y[0] = 0;

        let x[1] = 30;
        let y[1] = 0;

        let x[2] = 15;
        let y[2] = 0;

        let x[3] = 0;
        let y[3] = 0;

        let size = 15;
        do draw();
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void draw() {
        var int i;
        let i = 0;
        do Screen.setColor(true);

        while (i < length) {
            do Screen.drawRectangle(x[i], y[i], x[i] + size, y[i] + size);
            let i = i + 1;
        }

        return;
    }

    method void erase() {
        var int i;
        let i = 0;
        do Screen.setColor(false);

        while (i < length) {
            do Screen.drawRectangle(x[i], y[i], x[i] + size, y[i] + size);
            let i = i + 1;
        }

        return;
    }

    method void moveUp() {
        if (((y[0] - size) > 0) | ((y[0] - size) = 0)) {
            do erase();
            do shiftBody();

            let y[0] = y[0] - size;
            do draw();
        }
        return;
    }

    method void moveDown() {
        if (((y[0] + size) < (255 - size)) | ((y[0] + size) = (255 - size))) {
            do erase();
            do shiftBody();

            let y[0] = y[0] + size;
            do draw();
        }
        return;
    }

    method void moveLeft() {
        if (((x[0] - size) > 0) | ((x[0] - size) = 0)) {
            do erase();
            do shiftBody();

            let x[0] = x[0] - size;
            do draw();
        }
        return;
    }

    method void moveRight() {
        if (((x[0] + size) < (511 - size)) | ((x[0] + size) = (511 - size))) {
            do erase();
            do shiftBody();

            let x[0] = x[0] + size;
            do draw();
        }
        return;
    }

    method void shiftBody() {
        var int i;
        let i = (length - 1);
        while (i > 0) {
            let x[i] = x[i - 1];
            let y[i] = y[i - 1];
            let i = i - 1;
        }
        return;
    }
}